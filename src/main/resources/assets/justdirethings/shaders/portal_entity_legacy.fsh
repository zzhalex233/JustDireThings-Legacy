#version 120

#define PI 3.1415926538

uniform sampler2D Sampler0;
uniform float GameTime;
uniform int Layers;

varying vec2 texCoord0;

vec4 permute(vec4 x) {
    return mod(((x * 34.0) + 1.0) * x, 289.0);
}

vec4 taylorInvSqrt(vec4 r) {
    return 1.79284291400159 - 0.85373472095314 * r;
}

float snoise(vec3 v) {
    const vec2 C = vec2(1.0 / 6.0, 1.0 / 3.0);
    const vec4 D = vec4(0.0, 0.5, 1.0, 2.0);

    vec3 i = floor(v + dot(v, C.yyy));
    vec3 x0 = v - i + dot(i, C.xxx);

    vec3 g = step(x0.yzx, x0.xyz);
    vec3 l = 1.0 - g;
    vec3 i1 = min(g.xyz, l.zxy);
    vec3 i2 = max(g.xyz, l.zxy);

    vec3 x1 = x0 - i1 + C.xxx;
    vec3 x2 = x0 - i2 + C.yyy;
    vec3 x3 = x0 - D.yyy;

    i = mod(i, 289.0);
    vec4 p = permute(permute(permute(
        i.z + vec4(0.0, i1.z, i2.z, 1.0))
        + i.y + vec4(0.0, i1.y, i2.y, 1.0))
        + i.x + vec4(0.0, i1.x, i2.x, 1.0));

    float n_ = 1.0 / 7.0;
    vec3 ns = n_ * D.wyz - D.xzx;

    vec4 j = p - 49.0 * floor(p * ns.z * ns.z);
    vec4 x_ = floor(j * ns.z);
    vec4 y_ = floor(j - 7.0 * x_);

    vec4 x = x_ * ns.x + ns.yyyy;
    vec4 y = y_ * ns.x + ns.yyyy;
    vec4 h = 1.0 - abs(x) - abs(y);

    vec4 b0 = vec4(x.xy, y.xy);
    vec4 b1 = vec4(x.zw, y.zw);

    vec4 s0 = floor(b0) * 2.0 + 1.0;
    vec4 s1 = floor(b1) * 2.0 + 1.0;
    vec4 sh = -step(h, vec4(0.0));

    vec4 a0 = b0.xzyw + s0.xzyw * sh.xxyy;
    vec4 a1 = b1.xzyw + s1.xzyw * sh.zzww;

    vec3 p0 = vec3(a0.xy, h.x);
    vec3 p1 = vec3(a0.zw, h.y);
    vec3 p2 = vec3(a1.xy, h.z);
    vec3 p3 = vec3(a1.zw, h.w);

    vec4 norm = taylorInvSqrt(vec4(dot(p0, p0), dot(p1, p1), dot(p2, p2), dot(p3, p3)));
    p0 *= norm.x;
    p1 *= norm.y;
    p2 *= norm.z;
    p3 *= norm.w;

    vec4 m = max(0.6 - vec4(dot(x0, x0), dot(x1, x1), dot(x2, x2), dot(x3, x3)), 0.0);
    m = m * m;
    return 42.0 * dot(m * m, vec4(dot(p0, x0), dot(p1, x1), dot(p2, x2), dot(p3, x3)));
}

vec3 portalColor(int index) {
    if (index == 0) return vec3(0.032087, 0.078399, 0.090818);
    if (index == 1) return vec3(0.021892, 0.075924, 0.079485);
    if (index == 2) return vec3(0.037636, 0.081689, 0.090326);
    if (index == 3) return vec3(0.056564, 0.089883, 0.104838);
    if (index == 4) return vec3(0.074901, 0.097696, 0.087189);
    if (index == 5) return vec3(0.073761, 0.066895, 0.113646);
    if (index == 6) return vec3(0.094817, 0.091994, 0.156380);
    if (index == 7) return vec3(0.107489, 0.134120, 0.081064);
    if (index == 8) return vec3(0.116152, 0.111144, 0.175191);
    if (index == 9) return vec3(0.107721, 0.090188, 0.167229);
    if (index == 10) return vec3(0.143516, 0.118278, 0.138582);
    if (index == 11) return vec3(0.080006, 0.203332, 0.195792);
    if (index == 12) return vec3(0.166766, 0.112899, 0.184696);
    if (index == 13) return vec3(0.057281, 0.285338, 0.291970);
    if (index == 14) return vec3(0.174675, 0.360010, 0.272066);
    return vec3(0.098955, 0.294821, 0.621491);
}

void main() {
    vec2 st = texCoord0;
    vec3 color = vec3(0.0);
    float time = sin((2.0 * PI) * GameTime * 1.5);

    for (int i = 0; i < 16; i++) {
        if (i >= Layers) {
            break;
        }
        float index = float(i) + 1.0;
        color += texture2D(Sampler0, st + snoise(vec3(st, index * time))).rgb * portalColor(i);
    }

    gl_FragColor = vec4(color, 1.0);
}
